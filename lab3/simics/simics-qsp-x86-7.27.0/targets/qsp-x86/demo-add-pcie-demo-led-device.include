decl {
! Add a PCIe demo card to a machine setup

    group "Demo Card Setup"
        param master_system  : string or nil = NIL
        ! The name of the system inside of which we create the card
        param pcie_slot      : string or nil = NIL
        ! The PCIe slot to which to connect the card 
        param subsystem_name : string  = "demo_card"
        param device_type    : string  = "systemc"

    result card : string
}

##
## Generate name - inside master system if given, otherwise on the top level  
## 
if( $master_system != NIL ) {
    $name = ($master_system + "." + $subsystem_name)
} else {
    $name = $subsystem_name
}


##
## Add a card to the system, inside the $system
##
if( $device_type == "systemc" ) {
    load-module sc-leds-and-button-pcie-dev
    $card = ( create-sc-leds-and-button-pcie-comp $name )
} else {
    if( $device_type == "dml" ) {
        load-module leds_and_button_pcie_comp
        $card = ( create-leds-and-button-pcie-comp $name )
    }
}

##
## Connect to master system, if there is a master system to connect to
## - It will dynamically connect the new PCIe card to the system 
##
if( $pcie_slot != NIL ) {
    connect $pcie_slot $card.pci
}

# SystemC PCIe model requires attribute pci_bus set first
instantiate-components
