/*
  Â© 2010 Intel Corporation

  This software and the related documents are Intel copyrighted materials, and
  your use of them is governed by the express license under which they were
  provided to you ("License"). Unless the License provides otherwise, you may
  not use, modify, copy, publish, distribute, disclose or transmit this software
  or the related documents without Intel's prior written permission.

  This software and the related documents are provided as is, with no express or
  implied warranties, other than those that are expressly stated in the License.
*/

dml 1.2;

// Power Management (PM) Capability
template defining_pci_pm_capability {
    parameter pm_offset   default undefined;
    parameter pm_next_ptr default undefined;

    register pm_capability_header size 2 @ ($pm_offset + 0x0) {
        parameter documentation = "Capability Header";
    }
    register pm_capabilities      size 2 @ ($pm_offset + 0x2) {
        parameter documentation = "Power Management Capabilities";
    }
    register pm_status_control    size 2 @ ($pm_offset + 0x4) {
        parameter documentation = "Power Management Status and Control";
    }
    // TODO(ah): this register is reserved in gen5 spec
    register pm_sc_bridge         size 1 @ ($pm_offset + 0x6) {
        parameter documentation =
            "Power Management Control/Status Bridge Extensions";
    }
    register pm_data              size 1 @ ($pm_offset + 0x7) {
        parameter documentation = "Power Management Data";
    }

    register pm_capability_header {
        field id       [7:0]  is (constant) "Capability ID" {
            parameter value = 0x1;
        }
        field next_ptr [15:8] is (read_only) "Next Capability Pointer" {
            parameter hard_reset_value = $pm_next_ptr;
        }
    }
    register pm_capabilities {
        field v    [2:0]   is (read_only) "Version";
        field pmec [3:3]   is (read_only) "PME Clock";
        field irortd0 [4:4] is (read_only) "Immediate Readiness on Return to D0";
        field dsi  [5:5]   is (read_only) "Device Specific Initialization";
        field auxc [8:6]   is (read_only) "AUX Current";
        field d1s  [9:9]   is (read_only) "D1 Support";
        field d2s  [10:10] is (read_only) "D2 Support";
        field pmes [15:11] is (read_only) "PME Support";
    }
    register pm_status_control {
        field ps    [1:0]   is (read_write) "Power State";
        field nsr   [3:3]   is (ignore_write) "No Soft Reset";
        field pmee  [8:8]   is (read_write) "PME Enable";
        field dse   [12:9]  is (read_write) "Data Select";
        field dsc   [14:13] is (read_write) "Data Scale";
        field pmes  [15:15] is (write_1_clears) "PME Status";
    }
    register pm_sc_bridge {
        field bbs   [6:6] is (ignore_write) "B2/B3 Support";
        field bpcce [7:7] is (ignore_write) "Bus Power/Clock Control Enable";
    }
    register pm_data is (ignore_write);
}
