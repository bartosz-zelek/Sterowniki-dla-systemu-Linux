decl {
! Script to boot a Simics QSP target with Clear Linux
! And run program with sample-core-timin model connected (optionally)

    params from "%simics%/targets/qsp-x86/qsp-clear-linux.simics"
        default machine_name         = "machine"
        default num_cores            = 4
        default memory_megs          = 8Ki
        default show_con0            = TRUE
        default connect_real_network = "napt"

    group "Simulation"
	    param real_time_mode      : bool = TRUE
        ! Prevents the simulation from running faster than real time.
        ! Kick in once the target system has booted, since booting with
        ! real time mode is a waste of time.

	group "Timing"
	    param enable_timing_model : bool = TRUE
	    ! Runs with timing model enabled.
	    ! The timing model will be added to the system after boot
	    ! and a warmup run.
}

##
## Set up the base machine
##
run-script "%simics%/targets/qsp-x86/qsp-clear-linux.simics"

##
## Set system information
##
$system->system_info = "QSP x86 with Linux - Sample-core-timing Demo"

##
## Smart real-time-mode: wait until the boot is over, then enable it
## Allowing Simics to skip over the large amounts of idle time in the
## UEFI boot, but then allow users to use the target interactively
## without login time-outs.
##
if $real_time_mode {
    script-branch "Real time mode enabler" {
        bp.console_string.wait-for $system.serconsole.con "login: "
        add-session-comment "Login prompt spotted, enabling real-time-mode"
        enable-real-time-mode 200
    }
}

##
## Run md5sum, first to collect checksums and then optionally with timing model enabled
## to verify the collected checksums.
##
script-branch "Run test program" {
	# Warm disk-cache (in linux) and create a file with md5 checksums
    bp.console_string.wait-then-write $system.serconsole.con "simics@cl-qsp" "md5sum /bin/* > bin.md5\n"

    bp.console_string.wait-for $system.serconsole.con "simics@cl-qsp"
	if $enable_timing_model {
		add-session-comment "Adding timing model"
		(run-script "%simics%/targets/qsp-x86/sample-core-timing-setup.include"
		                  system = $system)
	}
	$system.serconsole.con.input "time md5sum -c --quiet --ignore-missing bin.md5\n"

	bp.console_string.wait-for $system.serconsole.con "simics@cl-qsp"

	if $enable_timing_model {
		$system.timing_tool.list-performance-metrics
		$system.timing_tool.list-metrics
	}
}
